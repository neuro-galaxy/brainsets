######################################################
# Allen Brain Observatory - Visual Coding OPhys (2016)
######################################################

DATASET = "allen_visual_coding_ophys_2016"

RAW_DIR = Path(config["RAW_DIR"]) / DATASET
PROCESSED_DIR = Path(config["PROCESSED_DIR"]) / DATASET
PIPELINE_DIR = Path(workflow.basedir)

with open(PIPELINE_DIR / "session_ids.txt") as fh:
    session_ids = [line.strip() for line in fh]

rule download_data:
    output:
        f"{RAW_DIR}/ophys_experiment_data/{{session_id}}.nwb"
    shell:
        f"""
        mkdir -p {RAW_DIR}
        python {PIPELINE_DIR}/download_data.py --output_dir {RAW_DIR} --session_id {{wildcards.session_id}}
        """


rule prepare_data:
    input:
        nwb_file = f"{RAW_DIR}/ophys_experiment_data/{{session_id}}.nwb"
    output:
        tmp = temp(f"{PROCESSED_DIR}/tmp/{{session_id}}.txt")
    log:
        f".snakemake/logs/{DATASET}/prepare_data.{{session_id}}.log"
    shell:
        f"""
        mkdir -p {PROCESSED_DIR}/tmp
        python {PIPELINE_DIR}/prepare_data.py --input_file {{input.nwb_file}} --output_dir {PROCESSED_DIR} >> {{log}}
        touch {{output.tmp}}
        """

rule all:
    input:
        expand(f"{PROCESSED_DIR}/tmp/{{session_id}}.txt", session_id=session_ids)
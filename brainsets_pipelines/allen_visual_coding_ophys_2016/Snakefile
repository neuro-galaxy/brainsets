######################################################
# Allen Brain Observatory - Visual Coding OPhys (2016)
######################################################

DATASET = "allen_visual_coding_ophys_2016"

RAW_DIR = config["RAW_DIR"]
PROCESSED_DIR = config["PROCESSED_DIR"]

with open(f"brainsets_pipelines/{DATASET}/session_ids.txt",) as fh:
    session_ids = [line.strip() for line in fh]

rule download_data:
    output:
        f"{RAW_DIR}/{DATASET}/ophys_experiment_data/{{session_id}}.nwb"
    shell:
        f"""
        mkdir -p {RAW_DIR}/{DATASET}
        python -m brainsets_pipelines.{DATASET}.download_data --output_dir {RAW_DIR}/{DATASET} --session_id {{wildcards.session_id}}
        """

rule prepare_data:
    input:
        nwb_file = f"{RAW_DIR}/{DATASET}/ophys_experiment_data/{{session_id}}.nwb"
    output:
        tmp = temp(f"{PROCESSED_DIR}/{DATASET}/tmp/{{session_id}}.txt"),
    log:
        f".snakemake/logs/{DATASET}/prepare_data.{{session_id}}.log"
    shell:
        f"""
        mkdir -p {PROCESSED_DIR}/{DATASET}/tmp
        python -m brainsets_pipelines.{DATASET}.prepare_data --input_file {{input.nwb_file}} --output_dir {PROCESSED_DIR}/{DATASET} >> {{log}}
        touch {{output.tmp}}
        """

rule all:
    input:
        expand(f"{PROCESSED_DIR}/{DATASET}/tmp/{{session_id}}.txt", session_id=session_ids)

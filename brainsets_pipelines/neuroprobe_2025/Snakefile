######################################################
# Neuroprobe Benchmark (Zahorodnii et al., 2025)
######################################################

DATASET = "neuroprobe_2025"

RAW_DIR = config["RAW_DIR"]
PROCESSED_DIR = config["PROCESSED_DIR"]
PIPELINE_DIR = Path(workflow.basedir)


checkpoint download_data:
    output:
        f"{RAW_DIR}/{DATASET}/manifest.txt"
    shell:
        f"""
        mkdir -p {RAW_DIR}/{DATASET}
        
        BASE_URL="https://braintreebank.dev"
        while read -r href; do
            [ -z "$href" ] && continue
            fname=$(basename "$href")
            wget -nc -O {RAW_DIR}/{DATASET}/"$fname" "$BASE_URL/$href"
        done < {PIPELINE_DIR}/download_hrefs_lite.txt

        for f in {RAW_DIR}/{DATASET}/*; do
            [ -e "$f" ] || continue
            case "$f" in
                *.zip)
                    echo "Extracting $f ..."
                    unzip -o -q "$f" -d {RAW_DIR}/{DATASET}
                    rm -f "$f"
                    ;;
                *)
                    ;;
            esac
        done
        
        find {RAW_DIR}/{DATASET}/ -type f -name "*.h5" | sed "s|^{RAW_DIR}/{DATASET}/||" | sed "s|^/*||" > {{output}}
        """

rule prepare_data:
    input:
        h5_file = f"{RAW_DIR}/{DATASET}/{{file}}"
    output:
        temp(f"{PROCESSED_DIR}/{DATASET}/tmp/{{file}}.txt")
    log:
        f".snakemake/logs/{DATASET}/prepare_data.{{file}}.log"
    shell:
        f"""
        mkdir -p {PROCESSED_DIR}/{DATASET}/tmp
        ROOT_DIR_BRAINTREEBANK={RAW_DIR}/{DATASET} python -m brainsets_pipelines.{DATASET}.prepare_data --input_file {{input.h5_file}} --output_dir {PROCESSED_DIR}/{DATASET} 2>&1 | tee {{log}}
        find {PROCESSED_DIR}/{DATASET}/ -type f -name "*.h5" | sed "s|^{PROCESSED_DIR}/{DATASET}//||" > {{output}}
        """

def aggregate_input(wildcards):
    with checkpoints.download_data.get(**wildcards).output[0].open() as manifest:
        files = [line.strip() for line in manifest]
    return expand(f"{PROCESSED_DIR}/{DATASET}/tmp/{{file}}.txt", file=files)


rule merge_manifests:
    input:
        aggregate_input
    output:
        f"{PROCESSED_DIR}/{DATASET}/manifest.txt"
    shell:
        f"""
        find {PROCESSED_DIR}/{DATASET}/ -type f -name "*.h5" | sed "s|^{PROCESSED_DIR}/{DATASET}//||" > {{output}}        
        """

rule all:
    input:
        f"{PROCESSED_DIR}/{DATASET}/manifest.txt"
